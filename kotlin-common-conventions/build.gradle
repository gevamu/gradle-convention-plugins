/*
 * Copyright 2023 Exactpro Systems Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java-gradle-plugin"
    id "maven-publish"
    id "com.gradle.plugin-publish"
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

static String gradlePluginImplementation(String id, String version) {
    return "${id}:${id}.gradle.plugin:${version}"
}

def substituteKotlinModule(DependencySubstitutions ds, String moduleNotation) {
    ds.substitute(ds.module(moduleNotation)).using(ds.module("$moduleNotation:$kotlin_version"))
}

dependencies {
    implementation(gradlePluginImplementation("org.jetbrains.kotlin.jvm", kotlin_version))
    // Kotlin style checker (3.0.x is for Kotlin 1.4)
    implementation(gradlePluginImplementation("org.jmailen.kotlinter", "3.0.2"))
    // Documentation engine for Kotlin
    implementation(gradlePluginImplementation("org.jetbrains.dokka", kotlin_version))
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substituteKotlinModule(it, "org.jetbrains.kotlin:kotlin-stdlib-jdk7")
        substituteKotlinModule(it, "org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    }
}

gradlePlugin {
    plugins {
        kotlinCommon {
            id = "${group}.kotlin-common-conventions"
            implementationClass = "${group}.KotlinCommonPlugin"
            displayName = "Kotlin Common Conventions"
            description = "Custom plugin setup"
        }
    }
    automatedPublishing = true
}

pluginBundle {
    website = vcs_url
    vcsUrl = vcs_url
    tags = ['gevamu']
}

